SUB CriaCalendario(CampoData, NomeCalendario)

    [$(NomeCalendario)]:
    LOAD
    	[$(CampoData)],
    	Year([$(CampoData)]) as Ano,
    	Month([$(CampoData)]) as Mes,
    	Date(MonthStart([$(CampoData)]), 'MMM/YYYY')as MesAno,
//     	Day([$(CampoData)]) as Dia,
//     	WeekDay([$(CampoData)]) as DiaSemana,//;
//     	Dual(Text(Date([$(CampoData)],'WWWW')),WeekDay([$(CampoData)])) as DiaSemanaCompleto,
//     	Date([$(CampoData)],'M') as MesN,
//     	Week([$(CampoData)]) AS Semana,
    	'Tri ' & ceil(month([$(CampoData)])/3) AS Trimestre,
    	ceil(month([$(CampoData)])/6) & '° Semestre' AS Semestre
	Where Exists([$(CampoData)]) // Comentar aqui caso precise do intervalo completo de datas e não apenas os valores de $(CampoData) que existe
    ;
    LOAD
    	Date(MenorData + IterNo()) as [$(CampoData)]
    	WHILE MenorData + IterNo() <= MaiorData;
    LOAD
    	Min(Data)-1 as MenorData,
    	Max(Data) 	as MaiorData;
    LOAD 
    	FieldValue('$(CampoData)', RecNo()) as Data
    AutoGenerate FieldValueCount('$(CampoData)');

END SUB;

//---------------- Depois dos dados carregados, executar a CALL da SUBrotina

Call CriaCalendario('OrderDate', 'CalendarioMestre')

