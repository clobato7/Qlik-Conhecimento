SUB CreateQVD(vTable,vDir,vSkipControl);
	
  // Salva o qvd
  STORE [$(vTable)] INTO [$(vDir)/$(vTable).qvd] (qvd);
  
	// Se 3o parametro (vSkipControl) não foi definido será feito um controle de carga
  	If IsNull(vSkipControl) then
	
        // Conta quantas linhas tem a tabela
        Let vNoOfRows = NoOfRows('$(vTable)');

        // Registra as informações para Controle de Carga
        // Tempos e Linhas de cada tabela
        ControleCargaDados:
        Load
            *,
        Interval(FimCarga - InicioCarga,'h:mm:ss')
                                            as TempoCarga;
        Load
          '$(vTable)'						as Tabela,
          Num('$(vNoOfRows)')				as Linhas,
          Timestamp(Now())					as FimCarga,
          Timestamp('$(vCheckPoint)')		as InicioCarga // obs: valor aproximado
        AutoGenerate 1;

        // Registra o CheckPoint (variavel global criada fora da sub rotina) para ser usado como InicioCarga da proxima tabela
		vCheckPoint = now();
  
  	EndIf
  
  // Dropa a Tabela
  DROP TABLE [$(vTable)];
  
  Trace ==== Generating QVD file of "$(vTable)" into "$(vDir)"... Done! ====;
EndSub;